MacroName Captures

Function Capture 1 0 CookFirst
   echo
   If $CookFirst == 0
      echo This macro performs an image stabilization calibration,
   Else
      echo This macro performs a high mag cook, image stabilization calibration,
   EndIf
   echo verifies the beam current is stable, focuses the scope and captures
   echo a montage.  
   echo 
   echo The Navigator table should contain the following entries for this macro:
   echo     Item1: The 2D region of interest to be captured
   echo     Item2: A point surrounded by at least 10um of empty formvar
   echo                  used for the current stability check
   echo     Item3: An optional point to focus upon.  The point should contain
   echo                  sufficient texture for autofocus to succeed.  If not specified 
   echo                  the center of the capture region will be used instead. 
   echo

   ReportNumTableItems
   NumNavItems = $reportedvalue1

   IF $NumNavItems < 1
      echo
      echo ERROR: Nav table does not have items described in help text above.
      echo Exiting
      return
   ELSEIF $NumNavItems < 2
      echo
      echo ERROR: Nav table does not have a point in empty formvar for filament stability check.
      echo Exiting
      return
   ENDIF

   ReportMag
   StartingMag = $reportedvalue2

   IF $reportedvalue2 == 1
      echo Attempting Capture in Low Mag mode, aborting. 
      echo Go to high magnification mode before using this macro.
      Return
   ENDIF

   NavItemFileToOpen  1
   echo $reportedvalue1

   #Prompt the user for the directory the capture will be placed
   EnterString CaptureDir Directory of the capture, relative to your data drive?
   #Prompt the user to create capture notes
   RunExternalTool 1
   ### FOCUS ###

   IF $NumNavItems < 3
      MoveToNavItem 1
   ELSE
      MoveToNavItem 3
   ENDIF

   Delay 3

   Focus

   ### CALIBRATE IMAGE SHIFT ###


   Try
      CalibrateImageShift
   Catch
      Echo "Image shift failed. Not interrupting capture per Jaap's recommendation."
   EndTry

   ScreenDown

   ReportAlpha
   ReportBeamShift
   ReportBeamTilt

   If $CookFirst == 1
      #### COOKING ####
      echo "Cooking Begins!"
      ReportSpotSize
      StartingSpotSize = $reportedvalue1

      echo Setting spot size for high-mag cook

      #SetSpotSize 2 #Commented for test of a lower power cook

      ReportSpotSize

      PreCookMontage $PrecookMontageD 2 0 0

      echo Restoring spot size after cook
      #SetSpotSize $StartingSpotSize

      echo "Cooking done!"
      reportclock
   EndIf

   ###  BEAM STABILITY CHECK ####
   #Move the stage to the area we've been told to use

   MoveToNavItem 2
   Try
      Call WaitForStableFilament - Python
   Catch
      Echo Error from WaitForStableFilament python. Trying regular version
      Call WaitForStableFilament
   EndTry
   ### Center on montage and capture ###
   MoveToNavItem 1

   CallFunction Notifications::SendStart
   Try
      Montage
   Catch
      CallFunction Notifications::SendMessage Montage failed with error on $ScopeName
      Return
   EndTry
   
   # Send the overview to Slack
   CallFunction Snapshot::TakeSnapshot 1 overview$CaptureDir

   # Copy the capture to DROPBOX
   CopyTarget = $CopyPath
   TargetDirName = $CaptureDir
   # Try first with a forward-slash to make CopyFunctionsV2 happy:
   Try
      CopySource = $DataPath/$CaptureDir
      CallFunction CopyFunctions::CopyDir
   # And in case that doesn't work, try with a backslash which should fall to the old way:
   Catch
      CopySource = $DataPath\$CaptureDir
      CallFunction CopyFunctions::CopyDir
   EndTry
   CallFunction Notifications::SendStop
EndFunction
